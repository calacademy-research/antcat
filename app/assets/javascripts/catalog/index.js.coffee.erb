panel_class = 'inline-form-panel'
panel_class_selector = '.' + panel_class
spinner_path = "<%= asset_path 'ui-anim_basic_16x16.gif' %>"

$ ->
  setup_page()
  setup_icons()
  setup_reference_keys()
  setup_forms()

  preload_images()

  setup_testing_or_development()

preload_images = ->
  (new Image()).src = spinner_path

setup_testing_or_development = ->
  if AntCat.testing
    $('.icon.edit').show()
  else
    if AntCat.environment is 'development'
      $('.icon.edit').click()

#--------------------------------------------------
setup_forms = ->
  $("#{panel_class_selector} div.form")
    .find('.submit')
      .live('click', submit_form)
    .end()
    .find('.cancel')
      .live('click', cancel_form)
    .end()
    .find('textarea')
      .taxt_edit_box()

#--------------------------------------------------
edit = ->
  return false if is_editing()
  $panel = $(this).closest panel_class_selector
  save_form $panel
  show_form $panel
  $('.taxt_edit_box', $panel).first().focus()
  false

show_form = ($panel, options) ->
  options = {} unless options
  $('div.display', $panel).hide()
  $('.icon').hide() unless AntCat.testing
  $('div.form', $panel).show()

submit_form = ->
  $form = $(this).closest('form')
  start_spinning $form
  $form.ajaxSubmit
    success: update_form
    error: handle_error
    dataType: 'json'
  false

start_spinning = ($form) ->
  $spinnerElement = $(':button', $form).parent()
  $spinnerElement.spinner position: 'left', img: spinner_path
  $(':button', $spinnerElement).attr 'disabled', 'disabled'

stop_spinning = ->
  $spinnerElement = $('.spinner')
  $(':button', $spinnerElement).removeAttr 'disabled'
  $spinnerElement.spinner 'remove'

update_form = (data, statusText, xhr, $form) ->
  stop_spinning()
  $panel = $('#item_' + (if data.isNew then "" else data.id))
  if not data.success
    show_error_messages $form, data.content
    return
  $panel.replaceWith data.content
  $panel = $('#item_' + data.id)
  $('div.form', $panel).hide()
  $('div.display', $panel).show().effect 'highlight', {}, 3000

show_error_messages = ($form, html) ->
  clear_error_messages $form
  $form.prepend $(html).find('ul.error_messages')

clear_error_messages = ($form) ->
  $('ul.error_messages', $form).remove()

handle_error = (jq_xhr, text_status, error_thrown) ->
  stop_spinning()
  alert "Oh, shoot. It looks like a bug prevented this item from being saved.\n\nPlease report this situation to Mark Wilden (mark@mwilden.com) and we'll fix it.\n\n#{error_thrown}" unless AntCat.testing

cancel_form = ->
  $panel = $(this).closest panel_class_selector
  clear_error_messages $panel
  unless $panel.attr('id') is 'item_'
    id = $panel.attr('id')
    restore_form $panel
    $panel = $('#' + id)
    $('div.form', $panel).hide()
    $('div.display', $panel).show().effect 'highlight', {}, 3000
  false

original_value_key = panel_class + '_original_value'

save_form = ($panel) ->
  $taxt_edit_box = $('textarea', $panel)
  $taxt_edit_box.data original_value_key, $taxt_edit_box.val()

restore_form = ($panel) ->
  $taxt_edit_box = $('textarea', $panel)
  $taxt_edit_box.val $taxt_edit_box.data original_value_key

is_editing = ->
  $("#{panel_class_selector} div.form").is ':visible'

#--------------------------------------------------
setup_page = ->
  set_dimensions()
  $(window).resize = set_dimensions

set_dimensions = ->
  set_height()
  set_width()

set_height = ->
  height = $('#page').height() -
    $('#site_header').height() -
    $('#page_header').height() - 2 -
    $('#page_notice').height() -
    $('#page_alert').height() -
    $('#search_results').height() - 3 - 2 - 2 -
    $('#taxon_key').height() - 2 -
    $('#site_footer').height() - 8
  $("#catalog").height(height)
  $("#catalog .index").height(height - $("#catalog .content").height())

set_width = ->
  $("#catalog .content").width($('#page').width())

#--------------------------------------------------
setup_reference_keys = ->
  $('.reference_key').live 'click', toggle_reference_key_expansion
  $('.reference_key_expansion_text').live 'click', toggle_reference_key_expansion

toggle_reference_key_expansion = ->
  $('.reference_key',           $(this).closest('.reference_key_and_expansion')).toggle()
  $('.reference_key_expansion', $(this).closest('.reference_key_and_expansion')).toggle()

#--------------------------------------------------
setup_icons = ->
  setup_icon_visibility()
  setup_icon_highlighting()
  setup_icon_click_handlers()

setup_icon_visibility = ->
  if not AntCat.testing
    $('.icon').hide()

  $('.history_item').live('mouseenter',
    ->
      unless is_editing()
        $('.icon', $(this)).show()
    ).live('mouseleave',
    ->
      $('.icon').hide()
    )

setup_icon_highlighting = ->
  $('.icon img').live('mouseenter',
    ->
      this.src = this.src.replace 'off', 'on'
    ).live('mouseleave',
    ->
      this.src = this.src.replace 'on', 'off'
    )

setup_icon_click_handlers = ->
  $('.icon.edit').live 'click', edit
