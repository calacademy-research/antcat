$ ->
  preload_images()
  space_out_images()
  $(window).resize space_out_images
  setup_login()
  setup_reference_keys()
  $('input[type=text]:visible:first').focus()

preload_images = ->
  for image in [
    '<%= image_path 'header_bg.png' %>'
    '<%= image_path 'antcat_logo.png' %>'
    '<%= image_path 'site_header_ant_5.png' %>'
    '<%= image_path 'ui-anim_basic_16x16.gif' %>'
    '<%= image_path 'collapsed.png' %>'
    '<%= image_path 'expanded.png' %>'
  ]
    (new Image()).src = image

AntCat.spinner_path = '<%= image_path 'ui-anim_basic_16x16.gif' %>'
AntCat.collapsed_image_path = '<%= image_path 'collapsed.png' %>'
AntCat.expanded_image_path = '<%= image_path 'expanded.png' %>'

AntCat.log = (message) ->
  unless typeof console == 'undefined'
    console.log message

AntCat.check = (caller, object_name, object) ->
  return if object and object.size() == 1
  AntCat.log "#{caller}: #{object_name}.size() != 1"

AntCat.check_nil = (caller, object_name, object) ->
  return if object
  AntCat.log "#{caller}: #{object_name} == nil"

space_out_images = ->
  total_image_width = 103 + 50 + 31 + 118 + 103 + 108
  image_count = 6
  available_width = $('#site_footer .images').width()
  margin_in_between = (available_width - total_image_width) / (image_count - 1)
  $("#site_footer .spacer").width margin_in_between

setup_login = ->
  $('#login .form').hide()
  $('#login a.link').click -> $('#login div').toggle()

#_something seems to override this method when it's named
# enable - it doesn't get called
$.fn.undisable = ->
  @.removeClass('ui-state-disabled')
   .removeAttr('disabled')

$.fn.disable = ->
  @.addClass('ui-state-disabled')
   .attr('disabled', 'true')

setup_reference_keys = ->
  $('.reference_key, .reference_key_expansion_text').on 'click', ->
    $(@).closest('.reference_key_and_expansion')
           .find('.reference_key, .reference_key_expansion')
           .toggle()
    false

# find just the topmost elements that match - don't drill down into them
$.fn.find_topmost = (selector) ->
  all_elements = @find(selector)
  all_elements.filter -> not all_elements.is $(@).parents()

# calling button('destroy') isn't getting through to the destroy function
# for some reason to do with jQuery UI's widget factory, so do this part
# manually
$.fn.unbutton = ->
  @.button 'destroy'
  @each ->
    button_text = $(@).find '.ui-button-text'
    return if button_text.length is 0
    $(@).text button_text.text()

$.fn.select = -> @.addClass 'ui-selecting'

AntCat.deselect = -> $('.ui-selecting').removeClass('ui-selecting')
